# √âcoute-moi, j'aimerais te parler

24 mars 2025

## Communication entre deux Arduino

<img src="../images/Capture-decran-le-2022-11-11-a-12.51.36.png" alt="" width="500" />

---

## 1 ‚Äì Mise en situation

Dans les exemples suivants, nous verrons comment transmettre des informations d'un ***Arduino*** vers un autre en utilisant le protocole s√©rie (**UART** / **RS-232** / **COM PORT**).

Le protocole s√©rie utilise deux broches pour l'√©change des donn√©es; une broche pour la r√©ception (**RX**) et une broche pour la transmission (**TX**)

Dans le cas d'un Arduino Uno ces broches sont **D0 (RX0)** et **D1 (TX0)**.

**L'Arduino Uno ne poss√®de qu'un seul port s√©rie.**

De plus, si nous utilisons la console s√©rie pour y afficher des messages alors le port s√©rie du **UNO** ne sera pas disponible pour le transfert d'information vers d'autres appareils.

**Ce n'est pas le cas pour tous les Arduino**.

Par exemple, l'**Arduino Mega** propose 4 ports s√©ries (UART0-UART3).

<img src="../images/Bluno_Mega_pinout-1024x521.png" alt="" width="500" />


---

### 1.1 ‚Äì Simulation d'un port s√©rie via la librairie SotfwareSerial

Il est possible d'utiliser des broches digitales standards pour reproduire le comportement et les fonctions d'un port UART en utilisant la librairie ***SotfwareSerial***. Cette librairie est disponible par d√©faut dans l'environnement de ***Arduino IDE***, c-a-d, qu'il n'est pas n√©cessaire de l'installer.

---

### 1.2 ‚Äì Connexion de deux appareils pour la communication via UART

Si nous d√©sirons relier deux appareils ensemble qui utiliseront leur port UART pour la communication, il faut relier la broche RX du premier sur la broche TX du deuxi√®me. Puis la broche TX du premier sur la broche RX du deuxi√®me.

<img src="../images/comh_asc-1024x594.jpg" alt="" width="500" />

Avec l'utilisation du Grove Hat, il sera possible d'inverser les broches RX/TX dans les param√®tres de configurations du port UART logiciel.

Par exemple, deux Arduino UNO reli√©s par un c√¢ble sur le port D4 du Hat Grove pourront √™tre configur√©s de la fa√ßon suivante:

```
SoftwareSerial portSerieTransmetteur(4, 5); // RX, TX
SoftwareSerial portSerieRecepteur(5, 4);    // RX, TX
```

---

## 2a ‚Äì Communication entre deux Arduino via UART physique

**NOTE**: Pour l'exemple suivant, il est important d'utiliser le c√¢ble avec les fils blanc et jaune invers√©s.

Il faut relier le Mega et le Uno de la fa√ßon suivante:

* Le port UART0 du Uno branch√© sur
* le port UART2 du Mega

### 2a.1 ‚Äì Le code du transmetteur (UNO)

```
/*
  Relier deux Arduino via le port UART physique
  Code du transmetteur - sur le UNO
  NOTE: Il est important d'utiliser le c√¢ble avec les fils blanc et jaune invers√©s
  */

#include <Arduino.h>
#include "rgb_lcd.h"
#include "Streaming.h"

#define UART_RX             6
#define UART_TX             7
#define UART_VITESSE        9600
#define MAX_NB_ALEATOIRE    99
#define UNE_SECONDE         1000
#define LCD_DEUXIEME_LIGNE  1
#define LCD_NB_LIGNE        2
#define LCD_NB_COL          16
#define transmetteur        Serial

rgb_lcd lcd;

//--------------------------------------------------------
void setup() {
  // port natif utilis√© pour la transmission vers le Mega
  transmetteur.begin(UART_VITESSE); 
  lcd.begin(LCD_NB_COL,LCD_NB_LIGNE);
  lcd.print("Transmetteur");
} // setup()

//--------------------------------------------------------
void loop() {
   // Transmettre un code √† chaque seconde  
   // g√©n√©rer un nombre entre 0 et MAX_NB_ALEATOIRE - 1
   byte unCode = random(MAX_NB_ALEATOIRE); 
   transmetteur.write(unCode);
   lcd.setCursor(0, LCD_DEUXIEME_LIGNE);
   lcd << "Envoi de: " << unCode;
   delay(UNE_SECONDE);
} // loop()

/*
[env:uno]
platform = atmelavr
board = uno
framework = arduino
lib_deps =
  seeed-studio/Grove - LCD RGB Backlight @ ^1.0.2
  mikalhart/Streaming @ ^1.0.0
*/
```

**NOTE** **‚Äî>** : Il est pr√©f√©rable que les deux Arduino ne soient pas reli√©s ensemble lors du t√©l√©versement vers le UNO. Le port '***Serial0***' sert √† programmer les Arduino.

---

### 2a.2 ‚Äì Le code du r√©cepteur (MEGA)

```
#include <Arduino.h>
/*
 Code du r√©cepteur - sur le Mega
*/

#define UART_VITESSE        9600
// Le RX du UNO doit √™tre connect√© au TX du MEGA et TX -> RX
// Utiliser le cable avec les fils jaune et blanc invers√©s
// Connecter le UNO sur le port Serial2 du MEGA
#define recepteur           Serial2 

//--------------------------------------------------------
void setup() {
  Serial.begin(UART_VITESSE);
  Serial.println("D√©marrage du r√©cepteur...");
  recepteur.begin(UART_VITESSE);
}

//--------------------------------------------------------
void loop() {  
  byte unCode;
  if (recepteur.available()) {
    unCode = recepteur.read();
    // afficher la valeur du byte re√ßu
    Serial.print("Code recu = ");
    Serial.println(unCode);
   } // si byte disponible sur le UART
}
```

---

## 2a.3 ‚Äì Laboratoire

<img src="../images/labo02.png" alt="" width="700" />

<br>

A ‚Äì Il faut modifier le code pr√©c√©dent pour permettre une transmission/r√©ception de 115200 bauds.

Est-ce que le montage fonctionne √† cette vitesse? Si oui, Est-ce que la r√©ception est fid√®le?

B ‚Äì Et pour une vitesse de 256000 bauds?

---

## 2b ‚Äì Connexion via SoftwareSerial

Pour le prochain laboratoire, il faut relier un **Arduino Mega** et un **Arduino UNO** en pla√ßant un c√¢ble entre les les connecteurs **D6 (UNO) et D10 (MEGA)**. Nous utiliserons une connexion UART simul√©e par logiciel en utilisant la librairie '***SoftwareSerial***'.

Rappel, il faut inverser les signaux:

```
SoftwareSerial portSerieTransmetteur(6, 7); // RX, TX
SoftwareSerial portSerieRecepteur(11, 10);  // RX, TX
```

### 2b.1 ‚Äì Code source du transmetteur ‚Äì le UNO

```
/*
  Relier deux Arduino via le port UART physique
  Code du TRANSMETTEUR - sur le UNO
*/

#include <Arduino.h>
#include "rgb_lcd.h"
#include "Streaming.h"
#include <SoftwareSerial.h>

#define UART_RX             6
#define UART_TX             7

#define UART_VITESSE        9600
#define MAX_NB_ALEATOIRE    99
#define UNE_SECONDE         1000
#define LCD_DEUXIEME_LIGNE  1
#define LCD_NB_LIGNE        2
#define LCD_NB_COL          16

rgb_lcd lcd;
SoftwareSerial transmetteur(UART_RX, UART_TX); // RX, TX

//--------------------------------------------------------
void setup() {
  Serial.begin(UART_VITESSE); // Disponible pour les traces dans cet exemple!
  Serial << "D√©marrage du transmetteur..." << endl;

  transmetteur.begin(UART_VITESSE); 
  lcd.begin(LCD_NB_COL,LCD_NB_LIGNE);
  lcd.print("Transmetteur");
} // setup()

//--------------------------------------------------------
void loop() {
   // Transmettre un code √† chaque seconde  
   // g√©n√©rer un nombre entre 0 et MAX_NB_ALEATOIRE - 1
   byte unCode = random(MAX_NB_ALEATOIRE); 
   Serial << "Envoi de: " << unCode << endl;
   transmetteur.write(unCode);
   lcd.setCursor(0, LCD_DEUXIEME_LIGNE);
   lcd << "Envoi de: " << unCode;
   delay(UNE_SECONDE);
} // loop()
```

### 2b.2 ‚Äì Code source du r√©cepteur ‚Äì Le Mega

```
/*
 Code du RECEPTEUR - sur le Mega
*/

#include <Arduino.h>
#include <SoftwareSerial.h>

#define UART_VITESSE        9600
// Le UNO et le MEGA sont reli√©s par les ports D6/D7 <-> D11/D10
// ATTENTION aux ports disponibles sur le MEGA pour SoftwareSerial  
#define UART_RX             10 
#define UART_TX             11

SoftwareSerial recepteur(UART_TX, UART_RX); // l'inverse du transmetteur

// --------------------------------------------------------
void setup() {
  Serial.begin(UART_VITESSE);
  Serial.println("D√©marrage du r√©cepteur...");
  recepteur.begin(UART_VITESSE);
}

// --------------------------------------------------------
void loop() {  
  byte unCode;
  if (recepteur.available()) {
    unCode = recepteur.read();
    // afficher la valeur du byte re√ßu
    Serial.print("Code recu = ");
    Serial.println(unCode);
   } // si byte disponible sur le UART
}
```

R√©sultat:

```
D√©marrage du r√©cepteur...
Code recu = 82
Code recu = 64
Code recu = 87
...
```

---

**NOTE IMPORTANTE:**

L'**Arduino MEGA** ne permet pas d'impl√©menter le **UART**, via la librairie ***SoftwareSerial***, en mode r√©ception sur toutes ses broches. Il faut se limiter aux broches suivantes:

* **10, 11, 12, 13, 14, 15, 50, 51, 52, 53, A8 (62), A9 (63), A10 (64), A11 (65), A12 (66), A13 (67), A14 (68) et A15 (69).**

Par contre, en mode transmission, il n'y a pas cette contrainte.

Il n'y pas non plus de contraintes si nous utilisons les ports natifs **UART** du **MEGA**.

De plus, sur certains **Arduino**, la vitesse **MAXIMUM** de transmission est limit√©e √† **57600bps**.

---

Documentation de la classe **::** [SoftwareSerial](https://docs.arduino.cc/learn/built-in-libraries/software-serial)

---

## 2.5 ‚Äì Laboratoire

<img src="../images/DependentAccomplishedEskimodog-size_restricted.gif" alt="" width="500" />

Il faut modifier les vitesses de transmission/r√©ception de l'exemple pr√©c√©dent √† 115 200 bauds et tester le syst√®me.

**Question**: Est-il toujours fonctionnel?

Et pour **57600**?

---

## 2.6 ‚Äì Laboratoire

<img src="../images/labo02.png" alt="" width="700" />

<br>

Il faut relier les deux Arduino par leur port natif **UART**; 1) Mega sur Serial3 et 2) UNO sur Serial.

**Note**: Il ne faut plus afficher dans la console s√©rie du projet UNO, seulement sur l'√©cran LCD.

**Question**: Est-ce que le syst√®me est toujours fonctionnel? Si non, il faut expliquer pourquoi.

```
// Exemple d'initialisation du UART no 3 sur le MEGA:
Serial3.begin(9600);
```

---

## 3 ‚Äì Transmission d'une chaine de caract√®res

La m√©thode Serial.print() est utilis√©e pour envoyer une chaine de caract√®res, de la m√™me fa√ßon que nous affichons dans le terminal s√©rie.

Pour la r√©ception, nous utiliserons le m√©thode [Serial.readString()](https://www.arduino.cc/reference/fr/language/functions/communication/serial/readstring/).

### 3.1 ‚Äì Code source du transmetteur (UNO)

```
/*
  Laboratoire 3: R√©ception/transmission d'une chaine de caract√®res
  Code du TRANSMETTEUR - sur le UNO
*/

#include <Arduino.h>
#include "rgb_lcd.h"
#include "Streaming.h"
#include <SoftwareSerial.h>

#define UART_RX             6
#define UART_TX             7

#define UART_VITESSE        9600
#define UNE_SECONDE         1000
#define LCD_DEUXIEME_LIGNE  1
#define LCD_NB_LIGNE        2
#define LCD_NB_COL          16

rgb_lcd lcd;
SoftwareSerial transmetteur(UART_RX, UART_TX); // RX, TX

const String messages[] = {
  "Haddock", 
  "Tournesol", 
  "Dupond et Dupont", 
  "Tintin",
  "Milou",
  "Bachi-bouzouk",
  "Boit-sans-soif",
  "Brontosaure",
  "Calembredaine",
  "Cataplasme",
  "Clysopompe",
  "Cornemuse",
  "Cornichon",
  "Cyclotron"
};

//--------------------------------------------------------
void setup() {
  Serial.begin(UART_VITESSE); // Disponible pour les traces dans cet exemple!
  Serial << "D√©marrage du transmetteur..." << endl;
  Serial << "Il y a " << sizeof(messages) / sizeof(messages[0]) << " messages disponibles." << endl;  
  transmetteur.begin(UART_VITESSE); 
  lcd.begin(LCD_NB_COL,LCD_NB_LIGNE);
  lcd.print("Transmetteur");
} // setup()

//--------------------------------------------------------
void loop() {
   // Envoyer un message al√©atoire √† toutes les secondes:
   byte unCode = random(sizeof(messages) / sizeof(messages[0]));
   transmetteur.print(messages[unCode]);
   Serial << "Envoi du message suivant: " << messages[unCode] << endl;
   lcd.setCursor(0, LCD_DEUXIEME_LIGNE);
   lcd << "Envoi du msg: " << unCode << " ";
   delay(UNE_SECONDE * 2);
} // loop()
```

R√©sultat:

```
D√©marrage du transmetteur...
Il y a 14 messages disponibles.
Envoi du message suivant: Brontosaure
Envoi du message suivant: Cataplasme
Envoi du message suivant: Boit-sans-soif
...
```

### 3.2 ‚Äì Code source du r√©cepteur (MEGA)

```
/*
 Laboratoire 3: R√©ception/transmission d'une chaine de caract√®res
 Code du r√©cepteur (MEGA)
 Version chaine de caract√®res
*/

#include <SoftwareSerial.h>
#include "Streaming.h"

// Le RX du UNO doit √™tre connect√© au TX du MEGA et TX -> RX
// Il faut donc inverser les signaux:
#define UART_RX             10
#define UART_TX             11
#define UART_VITESSE        9600
#define UNE_SECONDE         1000

SoftwareSerial recepteur(UART_TX, UART_RX); // RX, TX

// --------------------------------------------------------
void setup() {
  Serial.begin(UART_VITESSE);
  Serial.println("D√©marrage du r√©cepteur...");
  // Initialiser le port serie 'SoftwareSerial'
  recepteur.begin(UART_VITESSE);
}

// --------------------------------------------------------
void loop() {  
  String unMessage;
  if (recepteur.available()) {
    // delay(50); // attendre la r√©ception des donn√©es
    Serial << "Il y a " << recepteur.available() << " octets disponibles." << endl; 
    unMessage = recepteur.readString();
    // afficher les donn√©es lues
    Serial << "Message recu = " << unMessage << endl;
  } // si donn√©es disponibles sur le UART
}
// Fin du code
```

R√©sultat:

```
D√©marrage du r√©cepteur...
Il y a 2 octets disponibles.
Message recu = Cornemuse
Il y a 1 octets disponibles.
Message recu = Tournesol

// Note: Nous allons ins√©rer un petit d√©lai pour permettre au message d'√™tre enti√®rement re√ßu.

  if (recepteur.available()) {
    delay(50); // attendre la r√©ception des donn√©es

// R√©sultat:
D√©marrage du r√©cepteur...
Il y a 7 octets disponibles.
Message recu = Haddock
Il y a 13 octets disponibles.
Message recu = Calembredaine
```

---

## 3.5 ‚Äì üí° Transmission d'une structure de donn√©es

√âtant donn√© la structure suivante:

```
struct lesDonnees {
  int id;  // Occupe 2 octets en m√©moire.
  char ip[16];
  char description[10];
};  // longueur de la structure = 28 octets

// D√©finir un objet de type structure
lesDonnees mesDonnees = {1, "10.10.0.1", "E0000001"};
```

Voici un exemple de transmission d'une structure de donn√©es entre deux Arduino reli√©s par un port UART (UART2 du MEGA) et un SoftwareSerial (UNO).l

### 3.5.1 ‚Äì Le transmetteur ‚Äì (UNO sur SoftwareSerial:D6/D7)

```
/*
    Arduino UNO 
    Le transmetteur, via SoftwareSerial sur D7 et D6
*/

#include <Arduino.h>
#include <SoftwareSerial.h>
#include "Streaming.h"

#define DELAI_ENTRE_TRANSMISSION  1000

// D√©finir les pins pour la communication SoftwareSerial
SoftwareSerial lienAvecRecepteur(7, 6); // RX, TX

// ATTENTION: Le buffer de Serial √† un maximum de 64 octets.
struct lesDonnees {
  int id;
  char ip[16];
  char description[10];
};
// D√©finir un objet de type structure
lesDonnees mesDonnees = {1, "10.10.0.1", "E0000001"};

// ----------------------------------------------------------------
void setup() {
  // Initialiser la communication s√©rie avec l'ordinateur
  // Pour les traces au moniteur
  Serial.begin(9600);
  
  // Initialiser la communication SoftwareSerial vers le r√©cepteur
  lienAvecRecepteur.begin(57600);
  
  Serial << "D√©marrage de la transmission des donn√©es...\n";
} // setup

// ----------------------------------------------------------------
void loop() {
  static int compteur=0;
  mesDonnees.id = compteur++;
  Serial << "\nid: " << mesDonnees.id << "\nIP: " << mesDonnees.ip << "\nDescription: " << mesDonnees.description << endl;
  // Envoyer les donn√©es via SoftwareSerial
  // (byte*)&mesDonnees -> passe la localisation de la structure en m√©moire,
  // et indique de la traiter comme une s√©rie d'octets
  lienAvecRecepteur.write((byte*)&mesDonnees, sizeof(mesDonnees));
  Serial << "Octets envoy√©s: " << sizeof(mesDonnees) << endl;

  // √Ä l'occasion, simuler une erreur de transmission
  // avec une autre transmission sans d√©lai, de mesDonnees-1.
  if (random(0, 100) < 10) {
    lienAvecRecepteur.write((byte*)&mesDonnees, sizeof(mesDonnees)-1);
    Serial << "Erreur de transmission: " << sizeof(mesDonnees)-1 << " octets envoy√©s.\n";
    delay(DELAI_ENTRE_TRANSMISSION); 
  } // Ce qui va totaliser 55 (27+28) octets cot√© r√©cepteur, au lieu de 28 octets

  // Attendre un peu avant de renvoyer les donn√©es
  // Si ce d√©lai est trop court, il risque d'avoir une congestion √† niveau du r√©cepteur.
  // Faites des tests de fiabilit√©.
  delay(DELAI_ENTRE_TRANSMISSION); // 1 seconde d'attente
} // loop
```

<img src="../images/Capture-decran-le-2025-03-03-a-11.58.36.png" alt="" width="500" />

```
// Et dans le cas d'une erreur:
Erreur de transmission: 27 octets envoy√©s.
```

### 3.5.2 ‚Äì Le r√©cepteur (MEGA sur UART2)

```
/*
    Arduino MEGA
    Le r√©cepteur via UART2 (Serial2)
*/

#include <Arduino.h>
#include "Streaming.h"
#define leTransmetteur Serial2

// D√©finir la structure des donn√©es
// NOTE: tampon maximum en r√©ception UART 64 octets
struct lesDonnees {
  int id;
  char ip[16];           // Taille maximale de l'IP en format texte
  char description[10];  // Taille maximale de la description
};

lesDonnees mesDonnees;

// ----------------------------------------------------------------
void setup() {
  // Initialiser la communication s√©rie avec l'ordinateur
  Serial.begin(9600);
  
  // Initialiser la communication Serial2 (Mega utilise Serial2 pour la communication avec le transmetteur)
  leTransmetteur.begin(57600);

  Serial << "R√©ception des donn√©es...\n";
}

// ----------------------------------------------------------------
void loop() {
  if (leTransmetteur.available()) {
    // Attendre un peu, pour laisser le temps aux info d'arriver
    delay(100);
    Serial << "\nOctets re√ßus: " << Serial2.available() << endl;
 
    // V√©rifier si des donn√©es sont disponibles sur Serial2
    if (leTransmetteur.available() == (int) sizeof(mesDonnees)) {
      // Lire les donn√©es re√ßues
      leTransmetteur.readBytes((char*)&mesDonnees, sizeof(mesDonnees));

      // Afficher les donn√©es re√ßues
      Serial << "\nID: " << mesDonnees.id << "\nIP: " << mesDonnees.ip 
            << "\nDescription: " << mesDonnees.description << endl;
    } else {
      Serial << "Donn√©es incompl√®tes ou erron√©es.\n";
      while(leTransmetteur.available()) {
        leTransmetteur.read(); // Vider le tampon de r√©ception
      } 
    } // if sizeof(mesDonnees)
  } // if available()
} // loop()
```

<img src="../images/Capture-decran-le-2025-03-03-a-11.58.58.png" alt="" width="500" />

---

```
// En cas d'une erreur:
Octets re√ßus: 55
Donn√©es incompl√®tes ou erron√©es.
```

---

## 3.6 ‚Äì Laboratoire important üòâ (40 minutes)

<img src="../images/labo03.png" alt="" width="700" />

<br>

√Ä partir des √©l√©ments suivants:

* Un Arduino UNO : Le transmetteur
  + avec un module RTC sur I2C
* Un Arduino MEGA : Le r√©cepteur
  + avec un 4Digit display sur D6

Le transmetteur envoie vers le r√©cepteur, √† raison d'une fois par 900ms, l'heure en utilisant la structure de donn√©es suivante:

```
struct Temps {
  uint8_t heure;
  uint8_t minute;
  uint8_t seconde;
};

struct lesDonnees {
  int id;
  char ip[16];           // Taille maximale de l'IP en format texte
  char description[10];  // Taille maximale de la description
  Temps heure;
}; // 31 octets 

// Exemple d'utilisation
lesDonnees mesDonnees = {1, "10.10.0.1", "E0000001", {0, 0, 0}};
```

Le r√©cepteur affiche l'heure (**HH:MM**) re√ßue sur le **4digit** display et les informations suivantes sur le moniteur s√©rie:

```
Octets re√ßus: 31

ID: 1363
IP: 10.10.0.1
Description: E0000001
Heure 22:25:03
```

üëâ Le caract√®re '**:**', du **4Digit,** doit clignoter aux **500ms**, en traitement **non bloquant**.

**NOTE**: Vous pouvez travailler √† partir du code source de **3.5**

**ATTENTION** d'afficher correctement la valeur '0' -> 00:01

```
#include <TM1637V2.h> 
...
```

---

## 3.7 ‚Äì Laboratoire (30 minutes)

<img src="../images/labo02.png" alt="" width="700" />

<br>

Ajout d'une propri√©t√© pour l'**√©tat des capteurs** du **UNO** et d'un d√©lai de **5 secondes** pour la transmission des donn√©es.

√âtant donn√© les d√©clarations suivantes:

```
enum EtatAppareils {
  APP_LED1                 =     0b1 << 0, // 0000 0001
  APP_LED2                 =     0b1 << 1, // 0000 0010
  APP_RELAI                =     0b1 << 2, // 0000 0100
  APP_MOUVEMENT            =     0b1 << 3, // 0000 1000
  APP_MOUVEMENT_ARME       =     0b1 << 4, // 0001 0000
  APP_BOUTON_PANIQUE       =     0b1 << 5, // 0010 0000
  APP_RETRO_LCD            =     0b1 << 6, // 0100 0000
};

const char * texteAppareils[] = {
  "LED1",
  "LED2",
  "RELAI",
  "Mouvement",
  "Alarme",
  "Panique",
  "R√©tro LCD"
};

struct Appareils{
  unsigned int  etatAppareils;  // 2 octets
};

struct lesDonnees {
  int id;
  char ip[16];           // Taille maximale de l'IP en format texte
  char description[10];  // Taille maximale de la description
  Temps heure;
  Appareils etats; 
}; // 33 octets

// Et la d√©claration:
lesDonnees mesDonnees = {1, "10.10.0.1", "E0000001", {0, 0, 0}, {0b01010101}};
```

üëâ Modifier le labo pr√©c√©dent pour produire le r√©sultat suivant au niveau du r√©cepteur:

```
R√©ception des donn√©es...

------------------------------------
ID: 134
IP: 10.10.0.1
Description: E0000001
Heure 00:03:28
Etat appareils: 1001111
Appareil: LED1:                 üü¢
Appareil: LED2:                 üü¢
Appareil: RELAI:                üü¢
Appareil: Mouvement:            üü¢
Appareil: Alarme:               üî¥
Appareil: Panique:              üî¥
Appareil: R√©tro LCD:            üü¢

Octets re√ßus: 33
```

---

### üß® D√©fi suppl√©mentaire pour 3.7

Au niveau du transmetteur UNO, g√©n√©rer un valeur al√©atoire pour 'Appareils etats' √† chaque transmission.

---

## 4 ‚Äì Exemple d'un syst√®me d'alarme √† 2 Arduino

<img src="../images/10minutes.jpg" alt="" width="500" />

**NOTE**: Cet exemple est plus ou moins pertinent pour les sessions **H25-H26**.

---

### 4.1 ‚Äì Fichier d'ent√™te

```
/*
    Fichier:  code.alarme.h
    Auteur:   Alain Boudreault
    Date:     2021.11.21
    ---------------------------------------------------------
    Description:  D√©finir un type enum √† utiliser pour les
                  diff√©rents √©tats du syst√®me d'alarme.

                  Note: Inclure ce fichier dans le dossier de
                        votre projet.

                        Puis, programmer la syntaxe suivante 
                        pour utiliser ce type:

                        codesAlarme code;
                        ...
                        code = evenement_systeme_enligne;
    ---------------------------------------------------------
    M-A-J: 2022.11.11 A.B. - Ajout de 'evenement_nb_codes'
    ---------------------------------------------------------
*/

// S'assurer que ce fichier ne sera inclus qu'une seule fois lors de la compilation du projet
#ifndef LES_CODES_ERREUR_H
#define LES_CODES_ERREUR_H
enum codesAlarme {
  evenement_temperature_depassee,
  evenement_dectection_mouvement,
  evenement_code_invalide,
  evenement_code_valide,
  evenement_alarme_active,
  evenement_alarme_inactive,
  evenement_systeme_enligne,
  evenement_nb_codes
};

#endif // LES_CODES_ERREUR_H_ _
```

---

### 4.2 ‚Äì Code source du transmetteur

```
/*
    Simulation d'un syst√®me d'alarme UNO, qui envoie des √©v√©nements
    √† une centrale de controle (MEGA) via le protocole UART.

    Le UNO est connect√© par son port D8 au port UART2 du MEGA.

    √âtant donn√© que le UNO n'a qu'un seul port UART et qu'il est utilis√©
    pour d√©boger l'application, la librairie 'SoftwareSerial' est utilis√©e
    pour simuler un port UART √† partir d'une GPIO.

*/

#include <SoftwareSerial.h>
#include "code.alarme.h"
#include "rgb_lcd.h"
#include "Streaming.h"


rgb_lcd lcd;

// Le RX du UNO doit √™tre connect√© au TX du MEGA et