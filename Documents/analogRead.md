# Arduino ‚Äì AnalogRead

*Date: 13 septembre 2021*

## 1 ‚Äì Lecture d'une valeur analogique, analogRead()

L'Arduino Uno permet la lecture d'une valeur analogique √† partir des broches A0..A5.

Ce type d'entr√©e permet de lire la position, par exemple, du potentiom√®tre (contr√¥le de volume ou d'intensit√©) d'un gradateur pour une lumi√®re dans une pi√®ce de la maison.

Pour l'exemple suivant, nous aurons besoin de connecter le module Grove 'Potentiom√®tre' au connecteur A0 du Hat Grove.

**Note**: Les entr√©es analogiques de l'ATMEGA328P ont une r√©solution de 10 bits. C'est-√†-dire qu'une valeur entre 0 et 1023 sera retourn√©e lors de la lecture d'un senseur analogique.


<img src="../images/rotary-angle-sensor.jpg" alt="Potentiom√®tre rotatif" width="500" />


### 1.1 ‚Äì Exemple d'une lecture du potentiom√®tre

```cpp
/* 
   --------------------------------------------------------------------------
   Projet:         Introduction √† Arduino, la broche analogique.
   Nom du fichier: digitalRead.ino
   Auteur:         Alain Boudreault
   Date:           2022.09.10
   ---------------------------------------------------------------------------
   Description:    Petit programme qui lit la valeur d'un module analogique
                   (potentiom√®tre) et affiche la valeur dans la console s√©rie
   ---------------------------------------------------------------------------
   M-A-J:          2022.09.14 - Nom des constantes en caract√®res majuscules.
                   2025.03.03 - Ajout de Arduino.h + d√©lai d'affichage
   ---------------------------------------------------------------------------
*/
#include <Arduino.h>

//**********************************
// Les constantes
//**********************************
const int BROCHE_DU_SENSEUR     = A0;    // La broche du potentiom√®tre
const int BROCHE_DE_LA_DEL      = 3;     // La broche de la DEL.
const int VITESSE_UART          = 9600;  // Vitesse de la console s√©rie
const int DELAI_AFFICHAGE       = 150;  

//**********************************
void setup() {
  pinMode(BROCHE_DE_LA_DEL, OUTPUT);
  Serial.begin(VITESSE_UART);
} // setup()

//**********************************
void loop() {
  int valeurDuPotentiometre = 0;     
  // Lire et afficher la valeur du potentiom√®tre
  // NOTE: Les broches analogiques n'ont pas √† √™tre initialis√©es.
  valeurDuPotentiometre = analogRead(BROCHE_DU_SENSEUR);
  Serial.print("Valeur du pot: ");
  Serial.println(valeurDuPotentiometre);
  // Ajout d'un d√©lai pour ne pas saturer le moniteur s√©rie
  delay(DELAI_AFFICHAGE);
} // loop()

//****** FIN DU FICHIER
```

### 1.2 ‚Äì Tester l'exemple

**ATTENTION** √† la position de l'interrupteur de tension sur le Grove-Hat. Assurez-vous qu'il est √† la position 5 volts. L'ATmega328P utilise cette tension pour d√©terminer la valeur limite d'entr√©e d'une broche analogique. √Ä la position 3.3 volts, la valeur maximum sera autour de 710.

---

## 1.3 ‚Äì Laboratoire ‚Äì Clignoter une DEL en fonction du potentiom√®tre

<img src="../images/labo03.png" alt="" width="700" />

<br>

Il faut faire clignoter la **DEL** connect√©e sur **D3** en utilisant la valeur du **potentiom√®tre**.

Par exemple:

* Si le potentiom√®tre vaut 500 alors la DEL est **ON** 500ms puis **OFF** 500ms
* Si le potentiom√®tre vaut 150 alors la DEL est **ON** 150ms puis **OFF** 150ms

**ATTENTION**: La solution est affich√©e √† la section suivante. Si possible, r√©sistez √† la tentation de la consulter avant d'avoir tent√© un essai üòä.

---

### 1.3S ‚Äì Solution du laboratoire 1.3

```cpp
#include <Arduino.h>

// Les constantes
const int BROCHE_DU_SENSEUR     = A0;    // La broche du potentiom√®tre
const int BROCHE_DE_LA_DEL      = 3;     // La broche de la DEL.
const int VITESSE_UART          = 9600;

//**********************************
void setup() {
  pinMode(BROCHE_DE_LA_DEL, OUTPUT);
  Serial.begin(VITESSE_UART);
} // setup()

//**********************************
void loop() {
  int valeurDuPotentiometre = analogRead(BROCHE_DU_SENSEUR);

  Serial.print("Valeur du pot: ");
  Serial.println(valeurDuPotentiometre);
  
  // Allumer/√©teindre la DEL en utilisant la valeur du potentiom√®tre
  digitalWrite(BROCHE_DE_LA_DEL, HIGH);
  delay(valeurDuPotentiometre);
  digitalWrite(BROCHE_DE_LA_DEL, LOW);
  delay(valeurDuPotentiometre);

} // loop()

//****** FIN DU FICHIER
```

---

## 2 ‚Äì Laboratoire (dur√©e 15 minutes)

<img src="../images/labo03.png" alt="" width="700" />

<br>

Modifier le code source de l'exemple pr√©c√©dent pour que la DEL clignote seulement si la valeur du potentiom√®tre est comprise entre 200 et 800. Sinon, la DEL doit demeurer √©teinte.

Il faut d√©finir les valeurs limites avec des constantes (const int), c'est-√†-dire, il ne faut pas coder **200** et **800** en dur dans votre code.

---

## 3 ‚Äì üëâ Laboratoire (dur√©e 30 minutes)

√âcrire un programme qui d√©marre le clignotement d'une **DEL** si le bouton poussoir est appuy√© une fois et qui arr√™te le clignotement de la **DEL** √† la prochaine utilisation du bouton poussoir.

La vitesse de clignotement est renseign√©e par la valeur du potentiom√®tre.

**IMPORTANT**: Ne pas utiliser une solution bloquante pour le clignotement, ce qui aura comme effet de perdre des √©v√©nements du bouton.

üëâ Il ne faut pas que la **DEL** reste allum√©e si elle ne clignote pas.

**INDICE**: Voir [permutation bouton](http://ve2cuy.com/420-1c4/index.php/arduino-serial-printlnval-et-digitalread/#permutationbouton)

---

Voir la section 4.2 ‚Äì La fonction `map(value, fromLow, fromHigh, toLow, toHigh)`

---

## 4 ‚Äì Pulse Width Modulation (PWM), la fonction analogWrite()

Il est possible de simuler un signal de sortie de type analogique pour par exemple varier l'intensit√© d'une DEL ou bien la vitesse d'un moteur.

Cette simulation est obtenue en variant la dur√©e de l'√©tat allum√© et de l'√©tat √©teint.

Voici un graphe de d√©monstration:

<img src="../images/pwm.gif" alt="PWM Demonstration" width="500" />

R√©f√©rence: [Arduino.cc](https://www.arduino.cc/en/Tutorial/Foundations/PWM)

La fonctionnalit√© PWM est obtenue gr√¢ce √† la fonction **analogWrite(0..255)**.

**NOTE**: Sur [Arduino Uno](https://www.arduino.cc/reference/fr/language/functions/analog-io/analogwrite/), PWM est disponible seulement sur les broches **3, 5, 6, 9, 10 et 11**.

---

### 4.1 ‚Äì Varier l'intensit√© d'une DEL gr√¢ce √† PWM ‚Äì version 01

```cpp
#include <Arduino.h>

#define BROCHE_DEL      3         // LED sur pin 3
#define DELAI           20
#define UNE_SECONDE     1000

void setup() {
  pinMode(BROCHE_DEL, OUTPUT);    // Broche de la DEL en mode OUTPUT
} // setup()

void loop() {
    static byte intensiteDEL = 255;

    analogWrite(BROCHE_DEL, intensiteDEL); // Allumer la DEL avec le mode PWM
    intensiteDEL--;               // Diminuer la valeur d'intensit√© de la DEL

    if (intensiteDEL == 0) {      // Si intensit√© de la DEL est = 0 alors remettre √† 255
      intensiteDEL = 255;
      delay(UNE_SECONDE / 2);     // Laisser la DEL √©teinte pendant 1/2 sec.
    } // if (intensiteDEL == 0)

    delay(DELAI); // Ralentir un peu la variation d'intensit√© de la DEL.
} // loop()
```

### 4.1 ‚Äì Varier l'intensit√© d'une DEL gr√¢ce √† PWM ‚Äì version 02 (optimis√©e)

```cpp
int ledPin        = 3;         // LED sur pin 3
int intensiteDEL  = 255;
int delai         = 20;

void setup() {
  pinMode(ledPin, OUTPUT);    // Broche de la DEL en mode OUTPUT
}

void loop() {
    analogWrite(ledPin, --intensiteDEL);
    if (!intensiteDEL) { 
      intensiteDEL = 255;
      delay(500); // Laisser la DEL √©teinte pendant 1/2 sec.
    }
    delay(delai); // Ralentir un peu la variation d'intensit√© de la DEL.
}
```

---

### 4.2 ‚Äì La fonction map()

Il peut √™tre parfois utile de ramener une plage de valeurs, par exemple [0..1023] vers une autre plage de valeurs, par exemple [0..255].

Si nous voulions contr√¥ler l'intensit√© d'une DEL, en utilisant PWM, par un potentiom√®tre connect√© √† une source analogique, il faudrait concilier alors les deux plages de valeurs.

La librairie de d√©veloppement Arduino propose la fonction map() pour adresser cette situation.

### 4.3 ‚Äì Syntaxe de la fonction map()

```cpp
map(value, fromLow, fromHigh, toLow, toHigh)
```

### Param√®tres

* `value`: le nombre √† convertir  
* `fromLow`: la valeur minimum de l'√©tendue √† convertir  
* `fromHigh`: la valeur maximum de l'√©tendue √† convertir  
* `toLow`: la valeur minimum de l'√©tendue d√©sir√©e  
* `toHigh`: la valeur maximum de l'√©tendue d√©sir√©e

---

### 4.4.a ‚Äì Exemple d'utilisation de la fonction map() ‚Äì version Serial.print

```cpp
/*
    Fichier:  fn-map-println.ino
    Auteur:   Alain Boudreault
    Date:     2021.09.17
    --------------------------------------------------------------------------
    Description: Exemple d'une conversion des valeurs d'une entr√©e digitale
                 comprises entre 0..1023 vers une plage de valeurs
                 comprises entre 0..255.

                 Les valeurs converties sont affich√©es dans le terminal s√©rie
                 en utilisant Serial.print()
    --------------------------------------------------------------------------
    M-A-J:       2025.03.03 - Ajout de Arduino.h
    --------------------------------------------------------------------------    
*/

#include <Arduino.h>

// D√©finition des constantes
const int brocheDuSenseur  = A0;      // S√©lection de la broche du potentiom√®tre
const int vitesseUART      = 19200;   // Vitesse du port de communication s√©rie

// Les variables globales
int valeurDuPotentiometre = 0;

// ---------------------------------------
// Initialisation des param√®tres de d√©part
// ---------------------------------------
void setup() {
  Serial.begin(vitesseUART);
  Serial.print("Programme: fn-map-print.ino\n");
  Serial.print("Auteur:    Alain Boudreault\n");
  Serial.print("----------------------------\n");
  Serial.print("---- D√©but du programme ----\n");
  Serial.print("----------------------------\n\n");
} // setup()

// -------------------------------------
// Fonction principale de l'application
// -------------------------------------
void loop() {

  // Boucler sur la prochaine ligne tant que la valeur du Pot n'aura pas chang√© de plus de 1/1023. 
  while (abs(valeurDuPotentiometre - analogRead(brocheDuSenseur)) < 2);
  
  // Lire et stocker la valeur du potentiom√®tre
  // NOTE: Les broches analogiques n'ont pas √† √™tre initialis√©es.
  valeurDuPotentiometre = analogRead(brocheDuSenseur);
  
  // Convertir la plage des valeurs
  int valeurConvertieDuPotentiometre = map(valeurDuPotentiometre, 0, 1023, 0, 255);
  
  // Afficher les r√©sultats en utilisant Serial.print()
  Serial.print("Valeur du pot avant conversion: ");
  Serial.print(valeurDuPotentiometre);
  Serial.print(" apr√®s conversion: ");
  Serial.println(valeurConvertieDuPotentiometre);
   
  // Ajout d'un d√©lai pour ne pas saturer le moniteur s√©rie
  delay(500);
} // loop()
```

---

### Couvrir la section suivante seulement apr√®s avoir fait le module LCD

---

### 4.4.b ‚Äì Exemple d'utilisation de la fonction map() ‚Äì version IO Stream

```cpp
/*
    Fichier:  fn-map-stream.ino
    Auteur:   Alain Boudreault
    Date:     2021.09.17
    --------------------------------------------------------------------------
    Description: Exemple d'une conversion des valeurs d'une entr√©e digitale
                 comprises entre 0..1023 vers une plage de valeurs
                 comprises entre 0..255.

                 Les valeurs converties sont affich√©es dans le terminal s√©rie
                 en utilisant les Streams C++ (librairie de Mikal Hart)
    --------------------------------------------------------------------------
    M-A-J:       2025.03.03 - Ajout de Arduino.h
    --------------------------------------------------------------------------    
*/

#include <Arduino.h>
// https://github.com/janelia-arduino/Streaming
#include <Streaming.h>

// D√©finition des constantes
const int brocheDuSenseur  = A0;      // S√©lection de la broche du potentiom√®tre
const int vitesseUART      = 19200;   // Vitesse du port de communication s√©rie

// Les variables globales
int valeurDuPotentiometre = 0;

// ---------------------------------------
// Initialisation des param√®tres de d√©part
// ---------------------------------------
void setup() {
  Serial.begin(vitesseUART);
  Serial << "Programme: fn-map-stream.ino" << endl;
  Serial << "Auteur:    Alain Boudreault"  << endl;
  Serial << "----------------------------" << endl;
  Serial << "---- D√©but du programme ----" << endl;
  Serial << "----------------------------" << endl << endl;
} // setup()

// -------------------------------------
// Fonction principale de l'application
// -------------------------------------
void loop() {

  // Boucler sur la prochaine ligne tant que la valeur du Pot n'aura pas chang√© de plus de 1/1023. 
  while (abs(valeurDuPotentiometre - analogRead(brocheDuSenseur)) < 2);
  
  // Lire et stocker la valeur du potentiom√®tre
  // NOTE: Les broches analogiques n'ont pas √† √™tre initialis√©es.
  valeurDuPotentiometre = analogRead(brocheDuSenseur);
  
  // Convertir la plage des valeurs
  int valeurConvertieDuPotentiometre = map(valeurDuPotentiometre, 0, 1023, 0, 255);
  
  // Afficher les r√©sultats en utilisant les Streams C++
  Serial << "Valeur du pot avant conversion: " << valeurDuPotentiometre
         << " apr√®s conversion: " << valeurConvertieDuPotentiometre << endl;
   
  // Ajout d'un d√©lai pour ne pas saturer le moniteur s√©rie
  delay(500);
} // loop()
```

### 4.4.c ‚Äì Exemple d'utilisation de la fonction map() ‚Äì version sprintf()

```cpp
/*
    Fichier:  fn-map-sprintf.ino
    Auteur:   Alain Boudreault
    Date:     2021.09.17
    --------------------------------------------------------------------------
    Description: Exemple d'une conversion des valeurs d'une entr√©e digitale
                 comprises entre 0..1023 vers une plage de valeurs
                 comprises entre 0..255.

                 Les valeurs converties sont affich√©es dans le terminal s√©rie
                 en utilisant la fonction sprintf()
    --------------------------------------------------------------------------
    M-A-J:       2025.03.03 - Ajout de Arduino.h
    --------------------------------------------------------------------------    
*/

#include <Arduino.h>

// D√©finition des constantes
const int brocheDuSenseur  = A0;      // S√©lection de la broche du potentiom√®tre
const int vitesseUART      = 19200;   // Vitesse du port de communication s√©rie

// Les variables globales
int valeurDuPotentiometre = 0;
char chaine[80];

// ---------------------------------------
// Initialisation des param√®tres de d√©part
// ---------------------------------------
void setup() {
  Serial.begin(vitesseUART);
  Serial.print("Programme: fn-map-print.ino\n");
  Serial.print("Auteur:    Alain Boudreault\n");
  Serial.print("----------------------------\n");
  Serial.print("---- D√©but du programme ----\n");
  Serial.print("----------------------------\n\n");
} // setup()

// -------------------------------------
// Fonction principale de l'application
// -------------------------------------
void loop() {

  // Boucler sur la prochaine ligne tant que la valeur du Pot n'aura pas chang√© de plus de 1/1023. 
  while (abs(valeurDuPotentiometre - analogRead(brocheDuSenseur)) < 2);
  
  // Lire et stocker la valeur du potentiom√®tre
  // NOTE: Les broches analogiques n'ont pas √† √™tre initialis√©es.
  valeurDuPotentiometre = analogRead(brocheDuSenseur);
  
  // Convertir la plage des valeurs
  int valeurConvertieDuPotentiometre = map(valeurDuPotentiometre, 0, 1023, 0, 255);
  
  // Afficher les r√©sultats en utilisant sprintf()
  sprintf(chaine, "Valeur du pot avant conversion: %d, apr√®s conversion: %d", 
          valeurDuPotentiometre, valeurConvertieDuPotentiometre);
  Serial.println(chaine);
   
  // Ajout d'un d√©lai pour ne pas saturer le moniteur s√©rie
  delay(500);
} // loop()
```

R√©f√©rence: [sprintf()](http://www.cplusplus.com/reference/cstdio/sprintf/), [format](http://www.cplusplus.com/reference/cstdio/printf/)

**NOTE**: Pour des raisons de performance et d'optimisation de la m√©moire, la fonction *sprintf()* dans Arduino ne supporte pas les nombres r√©els (float). Une solution est disponible [ici](https://www.e-tinkers.com/2020/01/do-you-know-arduino-sprintf-and-floating-point/).


## 4.5 ‚Äì Laboratoire (dur√©e 15 minutes)

<img src="../images/labo02.png" alt="" width="700" />

<br>

√âcrire un programme qui permet de varier l'intensit√© d'une DEL connect√©e sur D6 √† partir d'un potentiom√®tre connect√© sur A2.

**Note**: Il faut ramener les valeurs du potentiom√®tre entre 0 et 255.

---

## 5 ‚Äì Varier l'intensit√© d'une DEL avec une boucle 'for'

**Rappel** ‚Äì Instruction 'for'

```cpp
for (int variable; condition tant que vrai; modification de la variable) inst; ou {bloc}

// Par exemple:

// Voici une boucle de 10 it√©rations (0 √† 9)
for (int i=0; i < 10; i++) {}
```

### 5.1 ‚Äì Voici un exemple complet d'une boucle 'for'

```cpp
void setup() {
  Serial.begin(9600);
  Serial.println("Debut du programme\n");

  // Boucle 1
  Serial.println("BOUCLE DE 0 A 9");
  for (int i = 0; i < 10; i++) {
    Serial.print(i);
    Serial.println(" - Yo!");
  }

  // Boucle 2
  Serial.println("\n\nBOUCLE DE 10 A 1");
  for (int i = 10; i > 0; i--) {
    Serial.print(i);
    Serial.println(" - Yo!");
  }
}

void loop() {
}
```

---

### 5.2 ‚Äì Varier l'intensit√© d'une DEL en utilisant une boucle 'for'

```cpp
// Varier l'intensit√© d'une DEL, connect√©e sur D3, en utilisant une boucle 'for'
#include <Arduino.h>

int ledPin = 3; 

void setup() {
  // Rien √† faire ici
  Serial.begin(9600);
}

void loop() {
  // Allumer graduellement la DEL en pas de 5 
  Serial.println("Allumer graduellement la DEL en pas de 5");
  for (int intensite = 0; intensite < 255; intensite += 5) {
    analogWrite(ledPin, intensite);
    delay(30);
  }

  // √âteindre graduellement la DEL en pas de 5 
  Serial.println("√âteindre graduellement la DEL en pas de 5");
  for (int intensite = 255; intensite >= 0; intensite -= 5) {
    analogWrite(ledPin, intensite);
    delay(30);
  }

  // delay(500); // Maintenir la DEL √©teinte 0,5 sec.
}
```

### 5.3 ‚Äì Tester l'application

---

## R√©f√©rences

[R√©f√©rence C++ de la librairie Arduino](https://www.arduino.cc/reference/fr/)

---

## Cr√©dits

*Document r√©dig√© par Alain Boudreault ¬© 2021-2026*  
*Version 2025.03.03.1 - R√©vision 2025.12.11.1*  

*Contenu par [VE2CUY](http://ve2cuy.com/blog)*